import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'de.undercouch.download' version '4.0.0'
    id 'idea'
}

allprojects {
    group 'com.poterion.monitor'
    version obtainVersion()

    apply plugin: 'org.jetbrains.kotlin.jvm'

    tasks.withType(JavaCompile.class) {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    tasks.withType(KotlinCompile.class) {
        kotlinOptions {
            freeCompilerArgs = [ "-Xjsr305=strict" ]
            jvmTarget = "11"
        }
    }

    repositories {
        mavenCentral()
        jcenter()
        google()
        maven { url 'https://plugins.gradle.org/m2/' }
        maven { url 'https://www.dcm4che.org/maven2/' }
    }
}

def obtainVersion() {
    def versionFile = new File('version')
    if (project.hasProperty('version') && project.version != "unspecified") {
        return project.version
    } else if (versionFile.exists() && versionFile.text != '') {
        return versionFile.readLines().first()
    } else {
        return '1.0-SNAPSHOT'
    }
}

task determineVersion(group: 'build setup', type: Exec) {
    commandLine "sh", "-c", '''\
        BUILD="\$(($(git tag | grep -E "v$(date +%Y)\\.[0-9]+" | sort -r | head -n1 | cut -d"." -f2) + 1))";
        if [[ $(git branch --show-current | grep "master") ]]; then
            SUFFIX=""
        else
            SUFFIX="-SNAPSHOT"
        fi
        VERSION="$(date +%Y).${BUILD}${SUFFIX}";
        echo "Next version: ${VERSION}";
        echo "${VERSION}" > version'''
}

task writePropertyFile(group: 'build') {
    doLast {
        def var = new Properties()
        File file = file("src/main/resources/application.properties")

        if (file.exists()) var.load(file.newDataInputStream())
        var.setProperty("version", "${version}")
        var.store(file.newWriter(), null)
    }
}

tasks.assemble.dependsOn(writePropertyFile)

task assembleFatJar(type: Jar, group: 'build', dependsOn: [assemble]) {
    manifest {
        attributes 'Implementation-Title': "Poterion Monitor",
                'Implementation-Version': archiveVersion,
                'Main-Class': "com.poterion.monitor.Main"
    }
    archiveBaseName.set(project.name + '-' + (project.hasProperty('flavor') ? project.flavor : 'all'))
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

dependencies {
    // Common
    api project(':api')
    compile project(':control')
    compile project(':ui')
    // Services
    compile project(':alert-manager')
    compile project(':gerrit-code-review')
    compile project(':jenkins')
    compile project(':jira')
    compile project(':sonar')
    compile project(':storyboard')
    compile project(':syndication-feed')
    // Notifiers
    compile project(':deployment-case')
    compile project(':devops-light')
    compile project(':notification-tabs')
    compile project(':notifications')
    compile project(':system-tray')
    // Kotlin
    compile 'org.jetbrains.kotlin:kotlin-reflect:1.4.10'
    // Commons
    compile 'commons-cli:commons-cli:1.4'
    // Logging
    compile "org.slf4j:slf4j-log4j12:1.7.30"
}

apply from: 'build-self-contained.gradle'
